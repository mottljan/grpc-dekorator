buildscript {
    ext {
        // Versions can not be used directly in plugin version declaration below so we need to store it to variable like this
        ksp_version = "${Versions.ksp}"
        protobuf_version = "${Versions.protobufPlugin}"
    }
}

plugins {
    id "com.google.devtools.ksp" version "$ksp_version"
    id "com.google.protobuf" version "$protobuf_version"
}

protobuf {
    protoc { artifact = Deps.protobufProtoc }
    plugins {
        grpc {
            artifact = Deps.gRpcJavaProtocGen
        }
        grpckt {
            artifact = Deps.gRpcKotlinProtocGen
        }
    }
    generateProtoTasks {
        all().forEach { task ->
            task.plugins {
                grpc {}
                grpckt {}
            }
        }
    }
}

kotlin.sourceSets.main {
    kotlin.srcDirs(
        // TODO below line is needed so far for AS to recognize generated sources used in manually
        //  written sources (https://github.com/google/ksp/issues/37)
        file("$buildDir/generated/ksp/main/kotlin"),
        // Including generated gRPC sources is needed for KSP to be able to resolve them during processing
        file("$buildDir/generated/source/proto/main/grpckt"),
        file("$buildDir/generated/source/proto/main/java")
    )
}

dependencies {
    implementation(project(":api"))
    ksp(project(":processor"))

    implementation(Deps.coroutines)
    implementation(Deps.coroutinesTesting)
    implementation(Deps.junitJupiter)
    implementation(Deps.kotlin)

    implementation(Deps.gRpcStub)
    implementation(Deps.gRpcKotlinStub)
    implementation(Deps.gRpcProtobuf)
    implementation(Deps.javaxAnnotation)
    implementation(Deps.protobufJavaUtil)

    testImplementation(Deps.kluent)

}

// Declares explicit dependency on generateProto task for kspKotlin, so we can be sure that gRPC code
// is generated before for KSP to be able to process that
afterEvaluate {
    def kspTaskName = "kspKotlin"
    def generateProtoTaskName = "generateProto"
    def kspTask = tasks.getByName(kspTaskName)
    def generateProtoTask = tasks.getByName(generateProtoTaskName)
    kspTask.dependsOn(generateProtoTask)
}
